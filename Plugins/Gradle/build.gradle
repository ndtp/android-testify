import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    id 'groovy'
    id 'kotlin'
    id 'org.jetbrains.dokka'
    id 'jacoco'
}

ext {
    pom = [
            publishedGroupId  : 'dev.testify',
            artifact          : 'dev.testify.gradle.plugin',
            libraryName       : 'testify-plugin',
            libraryDescription: 'Android screenshot instrumentation tests plugin.',
            siteUrl           : 'https://github.com/ndtp/android-testify',
            gitUrl            : 'https://github.com/ndtp/android-testify.git',
            licenseName       : 'The MIT License',
            licenseUrl        : 'https://opensource.org/licenses/MIT',
            author            : 'ndtp'
    ]
}

version = "$project.versions.testify"
group = pom.publishedGroupId
archivesBaseName = pom.artifact

sourceCompatibility = JavaVersion.VERSION_17

test {
    jacoco {
        enabled = true
    }
    finalizedBy jacocoTestReport
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

dependencies {
    implementation gradleApi()
    implementation libs.gradle
    implementation libs.kotlin.stdlib.jdk8

    testRuntimeOnly libs.junit.platform.launcher
    testImplementation gradleTestKit()
    testImplementation libs.junit.jupiter
    testImplementation libs.junit.jupiter.params
    testImplementation libs.mockk
    testImplementation libs.truth.v120
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
        freeCompilerArgs.add("-opt-in=kotlin.RequiresOptIn")
    }
}

jar {
    manifest {
        attributes "Implementation-Title": "Testify"
        attributes "Implementation-Vendor": "ndtp"
        attributes "Implementation-Version": archiveVersion
    }
}

tasks.register('configureLocal') {
    doFirst {
        version = "local"
        jar.manifest.attributes.put("Implementation-Version", version)
    }
}

tasks.register('copyLocal', Copy) {
    from "$buildDir/libs/${project.name}-local.jar"
    into 'jar'
}

tasks.register('assembleLocal') {
    dependsOn configureLocal
    dependsOn build
    dependsOn copyLocal

    build.mustRunAfter configureLocal
    copyLocal.mustRunAfter build
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.groovy.srcDirs
    archiveClassifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    archiveBaseName.set('testify')
    from dokkaJavadoc.outputDirectory
}

apply from: "../../publish.build.gradle"
apply from: '../../ktlint.gradle'

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
