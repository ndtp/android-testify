buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.github.dcendents.android-maven'
    id 'com.jfrog.bintray'
    id 'org.jetbrains.dokka'
}

def VERSION_NAME = "$project.versions.testify"

version = VERSION_NAME

android {
    compileSdkVersion coreVersions.shopify.compileSdk

    lintOptions {
        abortOnError true
        warningsAsErrors true
        textOutput 'stdout'
        textReport true
        xmlReport false
    }

    defaultConfig {
        minSdkVersion coreVersions.shopify.minSdk
        targetSdkVersion coreVersions.shopify.targetSdk
        versionCode 1
        versionName VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    dependencies {
        implementation depends.androidx.test.espresso.core
        implementation depends.androidx.test.rules
        implementation depends.androidx.test.runner
        implementation depends.kotlin.stdlib7

        testImplementation depends.mockito.v2
        testImplementation depends.mockito.kotlin

        androidTestImplementation depends.androidx.test.junit
        androidTestImplementation depends.androidx.test.runner
        androidTestImplementation depends.androidx.test.rules
        androidTestImplementation depends.androidx.test.espresso.core
        androidTestImplementation depends.androidx.test.espresso.contrib
    }
}

ext {
    pom = [
            publishedGroupId  : 'com.shopify.testify',
            artifact          : 'testify',
            libraryName       : 'testify',
            libraryDescription: 'Android screenshot instrumentation tests.',
            siteUrl           : 'https://github.com/Shopify/android-testify',
            gitUrl            : 'https://github.com/Shopify/android-testify.git',
            licenseName       : 'The MIT License',
            licenseUrl        : 'https://opensource.org/licenses/MIT'
    ]
}

group = pom.publishedGroupId

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId project.ext.pom.publishedGroupId
                artifactId project.ext.pom.artifact
                name project.ext.pom.libraryName
                description project.ext.pom.libraryDescription
                url project.ext.pom.siteUrl
                licenses {
                    license {
                        name project.ext.pom.licenseName
                        url project.ext.pom.licenseUrl
                    }
                }
                scm {
                    connection project.ext.pom.gitUrl
                    developerConnection project.ext.pom.gitUrl
                    url project.ext.pom.siteUrl
                }
            }
        }
    }
}

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = false
    reportUndocumented = false
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    baseName = 'testify'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

ext {
    bintray = [
            org        : 'shopify',
            repo       : 'shopify-android',
            name       : 'testify',
            allLicenses: ["MIT"]
    ]
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    // These values can be found on https://bintray.com/profile/edit
    user = System.getenv('BINTRAY_USER')
    // BINTRAY_USER : your personal profile name (from "Your Profile")
    key = System.getenv('BINTRAY_KEY') // BINTRAY_KEY : found on the left menu, under "API Key"

    configurations = ['archives']

    pkg {
        userOrg = project.ext.bintray.org
        repo = project.ext.bintray.repo
        name = project.ext.bintray.name
        desc = project.ext.pom.libraryDescription
        websiteUrl = project.ext.pom.siteUrl
        vcsUrl = project.ext.pom.gitUrl
        licenses = project.ext.bintray.allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = project.ext.pom.libraryDescription
        }
    }
}

bintrayUpload {
    dependsOn install
    doFirst {
        if (System.getenv('BINTRAY_USER').isEmpty()) {
            throw new IllegalArgumentException("BINTRAY_USER must be defined")
        }
        if (System.getenv('BINTRAY_KEY').isEmpty()) {
            throw new IllegalArgumentException("BINTRAY_KEY must be defined")
        }
    }
}

apply from: '../ktlint.gradle'