plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
}

ext {
    pom = [
            publishedGroupId  : 'dev.testify',
            artifact          : 'testify-ktx',
            libraryName       : 'testify-ktx',
            libraryDescription: 'Kotlin extension methods and helpers for Android Testify',
            siteUrl           : 'https://github.com/ndtp/android-testify',
            gitUrl            : 'https://github.com/ndtp/android-testify.git',
            licenseName       : 'The MIT License',
            licenseUrl        : 'https://opensource.org/licenses/MIT',
            author            : 'ndtp'
    ]
}

version = project.findProperty("testify_version") ?: "0.0.1-SNAPSHOT"
group = pom.publishedGroupId
archivesBaseName = pom.artifact

android {
    namespace "dev.testify.ktx"

    lintOptions {
        abortOnError true
        warningsAsErrors true
        textOutput 'stdout'
        textReport true
        xmlReport false
    }

    defaultConfig {
        compileSdk libs.versions.compileSdk.get().toInteger()
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    kotlinOptions {
        jvmTarget = '21'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    dependencies {
        implementation libs.androidx.monitor
        implementation libs.androidx.rules
        implementation libs.androidx.uiautomator
        implementation libs.core.ktx
        implementation libs.androidx.test.storage

        // TODO: trim this list down
        testImplementation libs.kotlinx.coroutines.test
        testImplementation libs.mockk
        testImplementation libs.slf4j.jdk14
        testImplementation libs.truth

        // TODO: trim this list down
        androidTestImplementation libs.androidx.appcompat
        androidTestImplementation libs.androidx.espresso.contrib
        androidTestImplementation libs.androidx.espresso.core
        androidTestImplementation libs.androidx.junit
        androidTestImplementation libs.androidx.multidex
        androidTestImplementation libs.androidx.rules
        androidTestImplementation libs.androidx.runner
        androidTestImplementation libs.material
        androidTestImplementation libs.mockk.android
    }
    packagingOptions {
        resources {
            excludes += [
                    'MANIFEST.MF',
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    apply from: "../../publish.build.gradle"
}
