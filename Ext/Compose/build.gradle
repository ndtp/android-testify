plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

ext {
    pom = [
            publishedGroupId  : 'dev.testify',
            artifact          : 'testify-compose',
            libraryName       : 'testify-compose',
            libraryDescription: 'Jetpack Compose extensions for Android Testify',
            siteUrl           : 'https://github.com/ndtp/android-testify',
            gitUrl            : 'https://github.com/ndtp/android-testify.git',
            licenseName       : 'The MIT License',
            licenseUrl        : 'https://opensource.org/licenses/MIT',
            author            : 'ndtp'
    ]
}

version = project.findProperty("testify_version") ?: "0.0.1-SNAPSHOT"
group = pom.publishedGroupId
archivesBaseName = pom.artifact

android {
    namespace "dev.testify.compose"

    lintOptions {
        abortOnError true
        warningsAsErrors true
        textOutput 'stdout'
        textReport true
        xmlReport false
    }

    defaultConfig {
        compileSdk libs.versions.compileSdk.get().toInteger()
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.14"
    }

    dependencies {
        implementation project(":Library")

        implementation(platform(libs.androidx.compose.bom))
        implementation libs.androidx.activity.compose
        implementation libs.androidx.appcompat
        implementation libs.androidx.core.ktx
        implementation libs.androidx.espresso.core
        implementation libs.androidx.lifecycle.runtime.ktx
        implementation libs.androidx.material
        implementation libs.androidx.rules
        implementation libs.androidx.runner
        implementation libs.androidx.ui
        implementation libs.androidx.ui.test.junit4
        implementation libs.androidx.ui.tooling.preview
    }

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    apply from: "../../publish.build.gradle"
}

apply from: '../../ktlint.gradle'
