plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

ext {
    pom = [
            publishedGroupId  : 'dev.testify',
            artifact          : 'testify-compose',
            libraryName       : 'testify-compose',
            libraryDescription: 'Jetpack Compose extensions for Android Testify',
            siteUrl           : 'https://github.com/ndtp/android-testify',
            gitUrl            : 'https://github.com/ndtp/android-testify.git',
            licenseName       : 'The MIT License',
            licenseUrl        : 'https://opensource.org/licenses/MIT',
            author            : 'ndtp'
    ]
}

version = "$project.versions.testify"
group = pom.publishedGroupId
archivesBaseName = pom.artifact

android {
    namespace "dev.testify.compose"
    compileSdkVersion coreVersions.compileSdk

    lintOptions {
        abortOnError true
        warningsAsErrors true
        textOutput 'stdout'
        textReport true
        xmlReport false
    }

    defaultConfig {
        minSdkVersion coreVersions.minSdk
        targetSdkVersion coreVersions.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${archivesBaseName}-${version}.aar"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${versions.compose.compilerExt}"
    }

    dependencies {
        implementation project(":Library")

        implementation "androidx.activity:activity-compose:${versions.androidx.activityCompose}"
        implementation "androidx.appcompat:appcompat:${versions.androidx.appCompat}"
        implementation "androidx.compose.material:material:${versions.compose.material}"
        implementation "androidx.compose.ui:ui-test-junit4:${versions.compose.ui}"
        implementation "androidx.compose.ui:ui-tooling-preview:${versions.compose.ui}"
        implementation "androidx.compose.ui:ui:${versions.compose.ui}"
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:${versions.androidx.lifecycleKtx}"
        implementation "androidx.test.espresso:espresso-core:${versions.androidx.test.espresso}"
        implementation "androidx.test:rules:${versions.androidx.test.rules}"
        implementation "androidx.test:runner:${versions.androidx.test.runner}"
    }
}

//task sourcesJar(type: Jar) {
//    from android.sourceSets.main.java.srcDirs
//    classifier = 'sources'
//}

afterEvaluate {
    apply from: "../../publish.build.gradle"
}

apply from: '../../ktlint.gradle'
